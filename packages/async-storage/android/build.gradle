buildscript {
    ext.getExtOrDefault = { name ->
        return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['AsyncStorage_' + name]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.7.2"
        // noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('KOTLIN_VERSION')}"
        classpath "com.google.devtools.ksp:symbol-processing-gradle-plugin:${getExtOrDefault('KSP_VERSION')}"
    }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "com.facebook.react"

// ksp/room is required to support legacy storage
apply plugin: 'com.google.devtools.ksp'

def getExtOrDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["AsyncStorage_" + name])
}

def isNewArchitectureEnabled() {
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

android {
    namespace "org.asyncstorage"

    compileSdkVersion getExtOrDefault("compileSdkVersion").toInteger()

    defaultConfig {
        minSdkVersion getExtOrDefault("minSdkVersion").toInteger()
        targetSdkVersion getExtOrDefault("targetSdkVersion").toInteger()
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        disable "GradleCompatible"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs += [
                    "generated/java",
                    "generated/jni"
            ]

            kotlin.srcDirs += [
                    "generated/java",
                    "generated/jni"
            ]

            if (isNewArchitectureEnabled()) {
                kotlin.srcDirs += ['src/newarch']
                java.srcDirs += ['src/newarch']
            } else {
                kotlin.srcDirs += ['src/oldarch']
                java.srcDirs += ['src/oldarch']
            }
        }
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation "com.facebook.react:react-android"
    implementation "org.asyncstorage.shared_storage:storage-android:1.0.0"


    // ksp/room is required to support legacy storage
    def room_version = getExtOrDefault("ROOM_VERSION")
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
}
